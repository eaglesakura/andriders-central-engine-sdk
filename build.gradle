// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        mavenCentral()
//        jcenter()
        maven {
            url "http://eaglesakura.github.io/maven/"
        }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.+'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'
        classpath "com.eaglesakura:eglibrary-gradle-plugin:0.1.21"
    }
}

allprojects {
    repositories {
        mavenCentral()
        maven {
            url "http://eaglesakura.github.io/maven/"
        }
    }
}

apply plugin: 'com.android.library'

// 0.4.2 : 獲得標高と傾斜に対応
def ACES_SDK_VERSION = "0.4.2";
def EGLIBRARY_ARTIFACT_VERSION = "0.1.27";
def PROTOBUF_ARTIFACT_VERSION = "2.6.0";

dependencies {
    compile fileTree(dir: 'libs', include: '*.jar')
    compile "com.google.protobuf:protobuf-java:${PROTOBUF_ARTIFACT_VERSION}"

    compile project(':eglibrary-android-java-core')
    compile project(':eglibrary-java-wrapper')
}

android {
    compileSdkVersion 22
    buildToolsVersion '22'

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 22

        // SDK VERSION
        buildConfigField "String", "ACE_SDK_VERSION", "\"${ACES_SDK_VERSION}\""
        buildConfigField "int", "ACE_PROTOCOL_VERSION", "1";
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src/main/generated/protobuf', 'src/main/java']
        }
    }
}

/**
 * Protocol Buffersファイルをビルドする
 */
task compileProtobuf(type: com.eaglesakura.gradle.tasks.ProtobufCompileTask) {
    src = file("src/main/protobuf")
    javaOutput = file("src/main/generated/protobuf")
}

task uploadSdkArchives << {
    tasks.uploadArchives.execute();
}

apply plugin: 'maven'

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: uri(new File("/dev-home/workspaces/github/maven")))
            pom.version = ACES_SDK_VERSION
            pom.groupId = "com.eaglesakura"
            pom.artifactId = "andriders-central-engine-sdk"
        }
    }
}

[uploadArchives.repositories.mavenDeployer]*.pom*.whenConfigured { pom ->
    pom.project {
        inceptionYear '2015'
        packaging 'aar'
        licenses {
            license {
                name 'NYSL'
                url 'https://raw.github.com/eaglesakura/eglibrary/master/LICENSE'
            }
        }

        dependencies {
            dependency {
                groupId = 'com.eaglesakura'
                artifactId = 'eglibrary-java-wrapper'
                version = EGLIBRARY_ARTIFACT_VERSION
                scope = 'compile'
            }
            dependency {
                groupId = 'com.eaglesakura'
                artifactId = 'eglibrary-android-java-core'
                version = EGLIBRARY_ARTIFACT_VERSION
                scope = 'compile'
            }
            dependency {
                groupId = 'com.google.protobuf'
                artifactId = 'protobuf-java'
                version = PROTOBUF_ARTIFACT_VERSION
                scope = 'compile'
            }
        }
    }
}

